grammar org.uis.lenguajegrafico.Lenguajegrafico with org.eclipse.xtext.common.Terminals

generate lenguajegrafico "http://www.uis.org/lenguajegrafico/Lenguajegrafico"

Lenguajegrafico:
	(elements+=Element)* ;

Element:
    Chart | Tuple | DashBoard;

Datatype:
	Text | Number
;

Chart:
	PieChart | LineChart | BarChart | MapChart | TableChart
;

Tuple:
	Tuple1 | Tuple2	| Tuple3 | Tuple4
;

VisualFeatures:
	Title | Legend 
;


Text:
	'String' name= ID ('=' value=STRING)?
;

Number:
	'Number' name= ID ('=' value=INT)?
;

URL:
	'URL' name=ID '=' value=STRING
;

//Visual Features
Title:
	'Title' '=' value=STRING
;

Labelx:
	'Label_x' '=' value=STRING
;

Labely:
	'Label_y' '=' value=STRING
;

Legend:
	'Legend''.'value=('True'|'False')//Default True
;

Hole:
	'Hole''.'value=('True'|'False')//Default False
;

Orientation:
	'Orientation''.'value=('Horizontal'|'Vertical')//Default Vertical
;

Area:
	'Area''.'value=('True'|'False')//Default False
;

ZoomLevel:
	'ZoomLevel''='value=(INT)//Range(0 - 15) Default 10
;

MakerType:
	'MakerType''.'value=('Normal'| 'Group'| 'MagnitudeCircle')// Default Normal
;

MapType:
	'MapType''.'value=('general'| 'normal'| 'terrain'| 'satellite' | 'hybrid')// Default General
;




//Charts Available
PieChart:
	'PieChart' name= ID '{'
		(tuple=[Tuple1])
	   	features+=(Title | Legend | Hole)*	   
	 '}'
;

BarChart:
	'BarChart' name= ID '{'
		(tuple=[Tuple2])
	   	features+=(Title | Legend | Labelx | Labely | Orientation)*	   
	 '}'
;

LineChart:
	'LineChart' name=ID '{'
	   (tuple=[Tuple2])
	   features+=(Title | Legend | Labelx | Labely | Area)*
	 '}'
;

MapChart:
	'MapChart' name =ID '{'
		(tuple=[Tuple3])
		features+=(Title | ZoomLevel | MakerType | MapType)*		
	'}'
;

TableChart:
	'TableChart' name=ID '{'
		(tuple=[Tuple4])
		features+=(Title)*		
	'}'
;

DashBoard:
	'DashBoard' name=ID '{'
	   features+=(Title)*
	   (charts+=[Chart])+
	'}'
;




//Tuples Availables
Tuple1:
	'Tuple1' name=ID '{'
	     (url=URL)
	     (value1=Text)
	     (value2=Number)
	'}'
;

Tuple2:
	'Tuple2' name=ID '{'
	     (url=URL)
	     (value1=(Text | Number))
	     (value2+=Number)+
	'}'
;

Tuple3:
	'Tuple3' name = ID '{'
	    (url=URL)
		(value1=Number)
		(value2=Number)
		(value3=(Text | Number))
	'}'
;

Tuple4:
	'Tuple4' name= ID '{'
		(url=URL)
		(values+=Datatype)+
	'}'
;

	
	








