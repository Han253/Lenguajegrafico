grammar org.uis.lenguajegrafico.Lenguajegrafico with org.eclipse.xtext.common.Terminals

generate lenguajegrafico "http://www.uis.org/lenguajegrafico/Lenguajegrafico"

Lenguajegrafico:
	(elements+=Element)* ;

Element:
    Chart | Tuple | DashBoard;

Datatype:
	Text | Number
;

Chart:
	PieChart | LineChart | BarChart | MapChart | TableChart
;

Tuple:
	Tuple1 | Tuple2	| Tuple3 | Tuple4
;

VisualFeatures:
	Title | Legend 
;


Text:
	'String' name= ID ('=' value=STRING)?
;

Number:
	'Number' name= ID ('=' value=INT)?
;

URL:
	'URL' name=ID '=' value=STRING
;

//Visual Features
Title:
	'Title' ':' value=STRING
;

Titlex:
	'Title_x' ':' value=STRING
;

Titley:
	'Title_y' ':' value=STRING
;

Legend:
	'Legend''.'value=('True'|'False')
;

Hole:
	'Hole''.'value=('True'|'False')
;




//Charts Available
PieChart:
	'PieChart' name= ID '{'
	   features+=(Title | Legend | Hole)*
	   (tuple=[Tuple1])
	 '}'
;

BarChart:
	'BarChart' name= ID '{'
	   features+=(Title | Legend | Titlex | Titley)*
	   (tuple=[Tuple2])
	 '}'
;

LineChart:
	'LineChart' name=ID '{'
	   features+=(Title | Legend)*
	   (tuple=[Tuple2])
	 '}'
;

MapChart:
	'MapChart' name =ID '{'
		features+=(Title)*
		(tuple=[Tuple3])
	'}'
;

TableChart:
	'TableChart' name=ID '{'
		features+=(Title)*
		(tuple=[Tuple4])
	'}'
;

DashBoard:
	'DashBoard' name=ID '{'
	   features+=(Title)*
	   (charts+=[Chart])+
	'}'
;




//Tuples Availables
Tuple1:
	'Tuple1' name=ID '{'
	     (url=URL)?
	     (value1=Text)
	     (value2=Number)
	'}'
;

Tuple2:
	'Tuple2' name=ID '{'
	     (url=URL)?
	     (value1=(Text | Number))
	     (value2+=Number)+
	'}'
;

Tuple3:
	'Tuple3' name = ID '{'
	    (url=URL)?
		(value1=Number)
		(value2=Number)
		(value3=Text)
	'}'
;

Tuple4:
	'Tuple4' name= ID '{'
		(url=URL)?
		(values+=Datatype)+
	'}'
;

	
	








